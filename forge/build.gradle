import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
	id 'dev.architectury.loom' version '0.12.0-SNAPSHOT'
	id 'maven-publish'
}
evaluationDependsOn(":sgui")

def javaTarget = 17;
java.toolchain.languageVersion = JavaLanguageVersion.of(javaTarget)

def props = project(":sgui")
archivesBaseName = rootProject.archives_base_name + "-" + project.name + "-" + props.minecraft_version
version = props.mod_version
group = rootProject.maven_group

loom {
	silentMojangMappingsLicense()

	forge {
		mixinConfigs = [
				"sgui.mixins.json"
		]
	}

	launches {
		data {
			arg "--existing", file("src/main/resources").absolutePath
		}
	}
}

dependencies {
	// to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${props.minecraft_version}"
	mappings "net.fabricmc:yarn:${props.yarn_mappings}:v2"
	// your forge dependency, this is **required** when using Forge Loom in forge mode!
	forge "net.minecraftforge:forge:${props.minecraft_version}-${rootProject.forge_version}"


	compileOnly project(":sgui")
	testCompileOnly("org.jetbrains:annotations:23.0.0")
}

tasks.withType(JavaCompile) {
	source(project(":sgui").sourceSets.main.allSource)
}

processResources {
	// define properties that can be used during resource processing
	inputs.property "version", project.version
	inputs.property "minecraft_version", props.minecraft_version

	filesMatching("META-INF/mods.toml") {
		expand("version": project.version,
				"minecraft_version": props.minecraft_version)
	}

	doLast {
		// Incredibly ugly, but I'm not willing to spend more days on this.
		fileTree(dir: outputs.files.asPath, include: "sgui.mixins.json").each { file -> {
			def mixinJson = new JsonSlurper().parse(file)
			mixinJson.putAt("refmap", "${archivesBaseName}-forge-refmap.json")
			return file.text = JsonOutput.toJson(mixinJson)
		}
		}
	}

	exclude("fabric.mod.json")


	from project(":sgui").sourceSets.main.resources
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"

	options.release = javaTarget

	sourceCompatibility = JavaVersion.toVersion(javaTarget)
	targetCompatibility = JavaVersion.toVersion(javaTarget)
	if (JavaVersion.current() < JavaVersion.toVersion(javaTarget)) {
		toolchain.languageVersion.set(JavaLanguageVersion.of(javaTarget))
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archives_base_name + "-" + project.name
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				classifier null
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}

}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	manifest {
		attributes([
				"Specification-Title"     : rootProject.archives_base_name,
				"Specification-Vendor"    : "",
				"Specification-Version"   : props.mod_version,
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : project.jar.archiveVersion,
				"Implementation-Vendor"   : "",
				"MixinConfigs"            : "${rootProject.archives_base_name}.mixins.json",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}
